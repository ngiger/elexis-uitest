--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Views
Element-Type: context
Element-Version: 2.0
Id: _P7r6AP-yEeiDKs20GBwclA
Runtime-Version: 2.5.4.202210020716
Save-Time: 11/19/22, 7:28 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

global [val viewPathName "" ] -override
global [ val defaultLanguage [ get-java-property "user.language" "de" ] ] -override

proc "WriteViewsCsvFile" {
	concat "WriteViewsCsvFile: " [ t menu_select_view ] " -> " [ t show_view ] | log
	get-menu -path [ t menu_select_view ]| click
	exec-with-options -command {
		get-window [ t show_view ] | get-tree | expand-all | get-table-data | write-csv-file $viewsCsvFile
	}
	wait 100
	get-window [ t show_view ] | get-button [ t cancel ] | click
	log "WriteViewsCsvFile: done"
}

global [val curViewSection ""] -override

/*
  viewName as seen in the CSV file
  Opens the view, maximizes it, creates a screenshot, resizes and closes it
*/
proc "check-one-view-item" [val viewName] {
	global [val viewPathName [replaceFirst $viewName "0," ""]] -override
	if [$viewName | str | matches "^0,.*"] {
		global [val curViewSection [replaceFirst $viewName "0," ""]] -override
		OpenAndResetStartPerspektive
	} -else {
		global [val viewPathName [replaceFirst $viewName "1," ""]] -override
		global [val realViewName [replaceFirst $viewName "1," ""]] -override
		global [val viewPathName2 [concat [$curViewSection] "/" $viewPathName]] -override
		global [val viewPathName [concat [$curViewSection] "/" $viewPathName]] -override
		global [val curViewImage [ concat $imagesDir
			"views/"
			[ replaceAll $viewPathName "\W" "_" ]
			".png"
			] ] -override
		concat "check-one-view-item "  $viewPathName | log
		try {
			open-window-by-name $viewPathName
			close-window-if-open "Ansicht veraltet"
			get-view $realViewName | maximize
			wait 100 // Give it some time to populate it
			get-eclipse-window | gen-screenshot -out [ $curViewImage ]
			get-view $realViewName | restore
		} -catch {
			concat "check-one-view-item: Failed opening: " $realViewName | log
		}
		try {
			get-view $realViewName | close
			}  -error  [val errorObj]  -catch {
				concat "check-one-view-item error closing: " $realViewName  " " $errorObj| log
			}
	}
}

proc "visit-all-views" [ val addToIgnorePattern "" ] {
	WriteViewsCsvFile
	read-lines $viewsCsvFile | foreach [val item] {
		concat "visit-all-views checks: " $item " addToIgnorePattern: " $addToIgnorePattern  | log
		if [$item | str | matches [
			concat  "^_indent.*" $addToIgnorePattern] ] {
			concat "Skipping first line and " $addToIgnorePattern " in visit all Views"| log
		} -else {
			if  [$item | str | matches "0,.*" ] {
 				check-one-view-item $item
			} -else {
 				check-one-view-item $item
			}
		}
	}
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
