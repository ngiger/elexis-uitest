--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _-OHP8BOEEemEb8FG0wciYA,_7pLWIBQVEemEb8FG0wciYA
Element-Name: EphaInteraktionen
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _tFxLkBNbEemKBfY3bI9wrQ
Runtime-Version: 2.4.2.201905080442
Save-Time: 7/28/19, 9:12 AM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Tests f端r die Epha-Interaktionen, welche neu via folgende Tickets dazugekommen sind:

* https://redmine.medelexis.ch/issues/14645 EPHA-Interaktionen in VerrechnungsDisplay integrieren
* https://redmine.medelexis.ch/issues/14780 EPHA-Interaktionen: Kein Text ausgeben, falls unterdr端ckt. Verrechnungstext wrappen

Die Benutzerdoku dazu findet man unter https://wiki.elexis.info/Medikamentenliste#Pr.C3.BCfung_auf_Interaktionen_zwischen_Medikamenten

F端r die Tests werden die folgenden Medis gebraucht

Paroxetin -> Losartan Keine Massnahmen notwendig
LOSARTAN, METOPROLOL Vorsichtsmassnahmen
Paroxetin -> Metoprolol Kombination vermeiden
Paroxetin -> Tamoxifen Kontraindiziert.


------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// TODO: Make it work with the demoDB
// TODO: Fix not displayin when we downloaded the interaction for the first time

// First we must setup the environment consisting of
// * small artikelstamm
// * Two patients to be able to switch between
// ** Armes Wesen ohne Fall
// ** Mille Tonnere mit Fall und Kons
// * One of them with an open consultation to be able to add artikelstamm items

OpenAndResetStartPerspektive
try {
	open-window-by-name [ t daten_artikel  ]
	with [get-view [ t artikel ]] {
	    get-tab-folder | get-tab-item Artikelstamm | click
	    with [get-tab-folder] {
	        key-type "M1+m"
	    }
	    get-table | get-item -path "ASPIRIN S Tabl 500 mg 20 Stk <8.7> " | get-property caption
			| equals "ASPIRIN S Tabl 500 mg 20 Stk <8.7>" | verify-true
	}
} -catch {
	log "Must read Artikelstamm"
	SetArtikelstammOrigin2oddb2ml
	ReadArtikelstamm
}

OpenAndResetStartPerspektive
SetEphaInteractionCheck true
if [SelectPatient Armes Wesen | eq false] {
	PatientenErstellen  Armes Wesene true "30.3.1979" "Nebenstrasse 33" 3320 Elexiskon "055 323 45 67"
}

if [SelectPatient Mille Tonnere| eq false] {
	PatientenErstellen  Mille Tonnere true "30.3.1979" "Nebenstrasse 33" 3320 Elexiskon "055 323 45 67"
}
if [FallOffen [ t case_UVG ] "201."] {
	log "Offenen Fall UVG gefunden"
} -else {
	FallZuPatientErstellen [ t case_UVG ] -caseReason [ t reason_UVG ] -startDate  "20.8.2018"
	log "Fall UVG 20.8.2018 erstellt"
}

SelectFall  [ t case_UVG ] "201"

if [ HasKonsSelected | eq false] {
	kons-erstellen "BasisTests f端r Mille Tonnere mit UVG"
} -else {
	log "HasKonsSelected returned true"
	AllVerrechnungenEntfernen
}

proc "EnsurePatientChanged" {
	// The change is only visible after closing the view or forcing a refresh by changing the patient
	SelectPatient Armes Wesen
	SelectPatient Mille Tonnere
}

kons-add-artikelstamm Paroxetin "Paroxetin.*20 mg 14 Stk.*"
VerifyInteractionStatus
get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
	gen-screenshot [concat $imagesDir "EphaInteractions/no_interaction.png"]

kons-add-artikelstamm LOSARTAN "LOSARTAN.*12.5.*"
EnsurePatientChanged // Is needed if it is the first time we downloaded the interaction. TODO: FIX IT!
VerifyInteractionStatus [ t interaction_no_relevant ]
get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
	gen-screenshot[concat $imagesDir "EphaInteractions/probably_no_interaction.png"]

kons-add-artikelstamm METOPROLOL "Metoprolol.*100 Stk.*"
VerifyInteractionStatus [ t interaction_periodic_control ]
VerifyInteractionLink
get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
	gen-screenshot [concat $imagesDir "EphaInteractions/check_interaction.png"]

kons-add-artikelstamm Nolvadex "Nolvadex.*100 Stk.*"
VerifyInteractionStatus [ t interaction_contra_indicated ]
VerifyInteractionLink
get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
	gen-screenshot [concat $imagesDir "EphaInteractions/contra_indicated.png"]

SetEphaInteractionCheck false
EnsurePatientChanged
VerifyInteractionStatus ""
get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |  
	gen-screenshot [concat $imagesDir "EphaInteractions/interaction_check_disabled.png"]

SetEphaInteractionCheck true
EnsurePatientChanged
VerifyInteractionStatus [ t interaction_contra_indicated ]
VerifyInteractionLink


// ENDE




------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
